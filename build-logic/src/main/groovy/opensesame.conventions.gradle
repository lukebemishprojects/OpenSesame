plugins {
    id 'java-library'
    id 'signing'
    id 'maven-publish'
    id 'opensesame.publishing'
    id 'opensesame.testing'
    id 'opensesame.base'
}

java.toolchain.languageVersion.set JavaLanguageVersion.of(17)

dependencies {
    compileOnly libs.jetbrains.annotations
    annotationProcessor(libs.autoservice)
    compileOnly(libs.autoservice)
}

processResources {
    from(rootProject.isolated.projectDirectory.file('LICENSE')) {
        into 'META-INF'
    }
}

jar {
    manifest {
        attributes([
                'Implementation-Version'   : (version as String),
                'Specification-Version'   : (version as String).split("-")[0],
                'Implementation-Commit-Time': managedVersioning.timestamp.get(),
                'Implementation-Commit': managedVersioning.hash.get(),
                'Automatic-Module-Name'   : "dev.lukebemish.opensesame.${project.name.replace("opensesame-", "")}".toString(),
                'FMLModType': 'LIBRARY'
        ])
    }
}
