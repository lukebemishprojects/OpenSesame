pluginManagement {
    repositories {
        gradlePluginPortal()
        maven {
            url "https://maven.lukebemish.dev/releases/"
        }
    }
    includeBuild 'build-logic'
}

plugins {
    id 'dev.lukebemish.central-portal-publishing' version '0.1.7' apply false
    id 'dev.lukebemish.managedversioning' version '2.0.0-beta.6'
    id 'dev.lukebemish.conventions' version '0.2.0-beta.2'
    id 'dev.lukebemish.zig-gradle' version '0.1.0'
}

managedVersioning {
    versionFile.set file('version.properties')
    versionPRs()
    versionSnapshots()

    publishing {
        mavenStaging()
        mavenSnapshot()
        mavenPullRequest()
        mavenCentralMakeBundle()
    }

    gitHubActions {
        release {
            prettyName = 'Release'
            workflowDispatch = true
            gradleJob {
                name = 'build'
                javaVersion.set '21'
                setupGitUser()
                readOnly = false
                gradlew 'Tag Release', 'tagRelease'
                gradlew 'Build', 'build'
                push()
                recordVersion 'Record Version', 'version'
                dependencySubmission()
            }
            gradleJob {
                name.set 'publishCentral'
                javaVersion.set '21'
                buildCache()
                needs.add('build')
                gradlew 'Publish', 'publish'
                tag.set('${{needs.build.outputs.version}}')
                sign()
                mavenCentral()
            }
            gradleJob {
                name.set 'publishPlugins'
                javaVersion.set '21'
                buildCache()
                needs.add('build')
                gradlew 'Publish Plugins', 'publishPlugins'
                tag.set('${{needs.build.outputs.version}}')
                sign()
                pluginPortal()
            }
        }

        def testOnBuild = { dev.lukebemish.managedversioning.actions.GradleJob job ->
            job.gradlew 'Assemble', 'assemble', ':testtargets:setupTestEnvironment'
            job.upload('platform-test-environment', ['testtargets/build/testEnvironment/*']) {
                requiredSteps.add 'assemble'
                with.put('retention-days', '1')
            }
            job.gradlew 'Test', 'check', '--continue'
            job.gradlew('Test Plugin', ['check', '--continue']) {
                workingDirectory.set './testplugin'
                runsOnError.set true
                requiredSteps.add 'assemble'
            }
            job.upload('junit-test-results-gradle', ['**/build/reports/test/open-test-report.xml']) {
                runsOnError.set true
                requiredSteps.add 'assemble'
                with.put('retention-days', '1')
            }
        }

        def platformTest = { dev.lukebemish.managedversioning.actions.GitHubAction action ->
            action.job {
                def strategy = [:]
                strategy['fail-fast'] = false
                def matrix = [:]
                strategy.matrix = matrix
                matrix.os = ['linux', 'windows', 'macos']
                // We're giving up on testing on x32 systems (windows or linux) because it's too much of a hassle -- we'd probably need custom JDK builds at some point...
                matrix.arch = ['x86_64', 'aarch64', 'arm']
                matrix.java = ['17', '21', '22']
                matrix.exclude = [
                        // No natives
                        [os: 'macos', arch: 'i386'],
                        [os: 'macos', arch: 'arm'],
                        [os: 'windows', arch: 'arm'],

                        // No way to test at present
                        [os: 'windows', arch: 'aarch64'],
                        [os: 'macos', arch: 'x86_64'],
                        [os: 'linux', arch: 'arm']
                ]
                matrix.include = [
                        [os: 'macos', runner: 'macos-latest'],
                        [os: 'windows', runner: 'windows-latest'],
                        [os: 'linux', runner: 'ubuntu-latest'],

                        [os: 'linux', arch: 'aarch64', java: '17', qemu: 'arm64', docker_arch: 'arm64', docker: 'arm64v8/eclipse-temurin:17'],
                        [os: 'linux', arch: 'aarch64', java: '21', qemu: 'arm64', docker_arch: 'arm64', docker: 'arm64v8/eclipse-temurin:21'],
                        [os: 'linux', arch: 'aarch64', java: '17', qemu: 'arm64', docker_arch: 'arm64', docker: 'arm64v8/eclipse-temurin:22'],
                ]
                parameters.put('strategy', strategy)
                name.set 'platform-test'
                needs.add('build')
                runsOn.set('${{ matrix.runner }}')
                step {
                    name.set 'Download Artifact'
                    id.set 'download'
                    uses.set 'actions/download-artifact@v4'
                    with.put 'name', 'platform-test-environment'
                }
                step {
                    name.set 'Setup QEMU'
                    uses.set 'docker/setup-qemu-action@v3'
                    parameters.put('if', '${{ matrix.qemu }}')
                }
                step {
                    name.set 'Setup Java'
                    parameters.put('if', '${{ !matrix.qemu }}')
                    uses.set 'actions/setup-java@v4'
                    with.put 'distribution', 'temurin'
                    with.put 'java-version', '${{ matrix.java }}'
                }
                step {
                    name.set 'Run with Docker'
                    parameters.put('if', '${{ matrix.docker }}')
                    run.set 'docker run --workdir "${GITHUB_WORKSPACE}" -v "${GITHUB_WORKSPACE}:${GITHUB_WORKSPACE}:z" --rm --platform linux/${{ matrix.docker_arch }} -t ${{ matrix.docker }} /bin/bash -c \'' +
                            'mkdir ../working; cp -r ./ ../working/; cd ../working; ' +
                            'java @args.txt; return=$?; ' +
                            'cp -r ./ ${GITHUB_WORKSPACE}/; cd ${GITHUB_WORKSPACE}; exit $return\''
                }
                step {
                    name.set 'Run'
                    parameters.put('if', '${{ !matrix.qemu }}')
                    parameters.put('shell', 'bash')
                    run.set 'java @args.txt'
                }
                step {
                    name.set 'Upload Results'
                    runsOnError.set true
                    requiredSteps.add 'download'
                    uses.set 'actions/upload-artifact@v4'
                    with.put 'name', 'junit-test-results-platform-${{ matrix.os }}-${{ matrix.arch }}-${{ matrix.java }}'
                    with.put 'path', 'results/open-test-report.xml'
                }
            }
            action.job {
                name.set 'aggregate-test-results'
                runsOn.set 'ubuntu-latest'
                needs.add('platform-test')
                getIf().set('always()')
                step {
                    name.set 'Download Platform Results'
                    id.set 'download'
                    uses.set 'actions/download-artifact@v4'
                    with.put 'pattern', 'junit-test-results-platform-*'
                }
                step {
                    name.set 'Upload Results'
                    uses.set 'actions/upload-artifact@v4'
                    with.put 'name', 'junit-test-results-platform'
                    with.put 'path', '**/open-test-report.xml'
                }
                step {
                    name.set 'Download Gradle Results'
                    runsOnError.set true
                    id.set 'download'
                    uses.set 'actions/download-artifact@v4'
                    with.put 'pattern', 'junit-test-results-gradle'
                }
                step {
                    name.set 'Find Reports'
                    id.set 'findreports'
                    run.set 'find . -name "open-test-report.xml" >> reports.txt'
                }
                step {
                    name.set 'Generate HTML Report'
                    id.set 'generate'
                    runsOnError.set true
                    requiredSteps.add 'findreports'
                    uses.set 'jbangdev/jbang-action@v0.126.3'
                    with.put 'script', 'org.opentest4j.reporting:open-test-reporting-cli:0.2.4:standalone@fatjar'
                    with.put 'scriptargs', 'html-report @reports.txt --output=report.html'
                }
                step {
                    name.set 'Upload Results'
                    runsOnError.set true
                    requiredSteps.add 'generate'
                    uses.set 'actions/upload-artifact@v4'
                    with.put 'name', 'test-report'
                    with.put 'path', 'report.html'
                }
            }
        }

        snapshot {
            prettyName.set 'Snapshot'
            workflowDispatch.set(true)
            onBranches.add 'main'
            gradleJob {
                buildCache()
                name.set 'build'
                javaVersion.set '21'
                testOnBuild.call(it)
                gradlew 'Publish', 'publish'
                mavenSnapshot('github')
            }
            platformTest.call(it)
        }
        build_pr {
            prettyName.set 'Build PR'
            pullRequest.set(true)
            gradleJob {
                javaVersion.set '21'
                name.set 'build'
                testOnBuild.call(it)
                gradlew 'Publish', 'publish'
                pullRequestArtifact()
            }
            platformTest.call(it)
        }
        publish_pr {
            concurrency.unsetConvention()
            concurrency.unset()
            prettyName.set 'Publish PR'
            publishPullRequestAction(
                    'github',
                    "dev/lukebemish/opensesame/opensesame-*",
                    'Build PR'
            )
        }
        report {
            prettyName.set 'Report Test Results'
            concurrency.unsetConvention()
            concurrency.unset()
            completedWorkflows.set(['Build PR', 'Snapshot'])
            job {
                name.set 'checks'
                permissions.put('contents', 'read')
                permissions.put('actions', 'read')
                permissions.put('checks', 'write')
                step {
                    name.set 'Checkout'
                    uses.set 'actions/checkout@v4'
                    with.put 'ref', '${{ github.event.workflow_run.head_commit.id }}'
                    with.put 'repository', '${{ github.event.workflow_run.head_repository.full_name }}'
                }
                step {
                    name.set 'Download Gradle Test Report'
                    id.set 'download-gradle'
                    uses.set 'actions/download-artifact@v4'
                    with.put 'name', 'junit-test-results-gradle'
                    with.put 'path', 'gradle'
                    with.put 'github-token', '${{ github.token }}'
                    with.put 'run-id', '${{ github.event.workflow_run.id }}'
                }
                step {
                    name.set 'JUnit Test Report - Transformation'
                    runsOnError.set true
                    requiredSteps.add 'download-gradle'
                    uses.set 'dorny/test-reporter@v1'
                    with.put 'name', 'Test Results - Transformation'
                    with.put 'path', 'gradle/**/TEST-*.xml'
                    with.put 'reporter', 'java-junit'
                    with.put 'fail-on-empty', 'true'
                    with.put 'list-tests', 'failed'
                }
                step {
                    name.set 'Download Platform Test Report'
                    id.set 'download-platform'
                    runsOnError.set true
                    uses.set 'actions/download-artifact@v4'
                    with.put 'name', 'junit-test-results-platform'
                    with.put 'path', 'platform'
                    with.put 'github-token', '${{ github.token }}'
                    with.put 'run-id', '${{ github.event.workflow_run.id }}'
                }
                step {
                    name.set 'JUnit Test Report - Metafactory'
                    runsOnError.set true
                    requiredSteps.add 'download-platform'
                    uses.set 'dorny/test-reporter@v1'
                    with.put 'name', 'Test Results - Metafactory'
                    with.put 'path', 'platform/**/TEST-*.xml'
                    with.put 'reporter', 'java-junit'
                    with.put 'fail-on-empty', 'true'
                    with.put 'list-tests', 'failed'
                }
            }
        }
    }
}

gradle.lifecycle.beforeProject {
    tasks.withType(Sign).configureEach {
        // Gradle broke something in 8.8 here, and now you can't publish plugins to other mavens if signing is disabled...
        enabled System.getenv('GPG_KEY') !== null
    }
    
    pluginManager.withPlugin('publishing') {
        publishing {
            publications.configureEach {
                managedVersioning.sign(signing, it)
            }
        }
    }
}

dependencyResolutionManagement {
    repositories {
        mavenCentral()
        maven {
            name = 'FabricMC'
            url = 'https://maven.fabricmc.net/'
        }
        maven {
            name = 'SpongePowered'
            url = 'https://repo.spongepowered.org/maven'
        }
        gradlePluginPortal()
    }
    repositoriesMode = RepositoriesMode.FAIL_ON_PROJECT_REPOS
}

rootProject.name = 'opensesame'

def subprojects = [
        'core',
        'natives',
        'compile',
        'groovy',
        'javac',
        'plugin-core',
        'plugin-loom',
        'fabric',
        'mixin'
]

include subprojects

subprojects.each {
    def p = project(":$it")
    p.projectDir = file(it.replace('-', '/').replace(':', '/'))
    p.name = "opensesame-$it".split(':')[-1]
}

include 'testtargets'
