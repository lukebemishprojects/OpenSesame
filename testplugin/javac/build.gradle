plugins {
    id 'java'
    id 'dev.lukebemish.opensesame'
}

group='dev.lukebemish.opensesame'

java.toolchain.languageVersion.set(JavaLanguageVersion.of(17))

configurations {
    testSource {
        canBeResolved = true
    }
}

repositories {
    mavenCentral()
    maven {
        url = "https://maven.lukebemish.dev/releases/"
    }
}

dependencies {
    testSource('dev.lukebemish.opensesame:testtargets') {
        capabilities {
            requireFeature 'transformation-sources'
        }
    }

    testImplementation 'dev.lukebemish.opensesame:opensesame-core'
    testImplementation 'dev.lukebemish.opensesame:testtargets'
    testImplementation libs.asm.core
}

opensesame.applyJavac(sourceSets.test)

tasks.named('compileTestJava', JavaCompile).configure {
    dependsOn(configurations.testSource)
    source(configurations.testSource)
}

test {
    useJUnitPlatform()

    testLogging {
        showStandardStreams = true
        exceptionFormat = 'full'
        showCauses = true
        showStackTraces = true
        events = ['passed', 'failed', 'skipped']
    }

    enableAssertions = true
}
