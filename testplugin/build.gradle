plugins {
    id 'java'
    id 'fabric-loom' version libs.versions.fabric.loom apply false
    id 'dev.lukebemish.opensesame'
    id 'dev.lukebemish.opensesame.loom' apply false
}

group='dev.lukebemish.opensesame'

java.toolchain.languageVersion.set(JavaLanguageVersion.of(17))

configurations {
    testSource {
        canBeResolved = true
    }
}

repositories {
    mavenCentral()
}

dependencies {
    testSource('dev.lukebemish.opensesame:testtargets') {
        capabilities {
            requireFeature 'transformation-sources'
        }
    }
}

opensesame.apply(sourceSets.test)

tasks.named('compileTestJava', JavaCompile).configure {
    dependsOn(configurations.testSource)
    source(configurations.testSource)
}

testing {
    suites {
        test {
            targets.configureEach {
                testTask.configure {
                    useJUnitPlatform()

                    testLogging {
                        showStandardStreams = true
                        exceptionFormat = 'full'
                        showCauses = true
                        showStackTraces = true
                        events = ['passed', 'failed', 'skipped']
                    }

                    enableAssertions = true
                    systemProperty('junit.platform.reporting.open.xml.enabled', 'true')
                    systemProperty('junit.platform.reporting.output.dir', 'build/reports/test')
                    systemProperty('opensesame.transformer.name', 'gradle')
                    reports {
                        junitXml.required = false
                        html.required = false
                    }
                }
            }

            dependencies {
                runtimeOnly libs.junit.engine
                implementation 'dev.lukebemish.opensesame:opensesame-core'
                implementation 'dev.lukebemish.opensesame:testtargets'
                implementation libs.asm.core
                implementation libs.junit.api
            }
        }
    }
}
