plugins {
    id 'java'
    id 'fabric-loom'
    id 'dev.lukebemish.opensesame.loom'
}

group='dev.lukebemish.opensesame'

java.toolchain.languageVersion.set(JavaLanguageVersion.of(17))

loom {
    runs.configureEach {
        ideConfigGenerated false
    }
}

repositories {
    mavenCentral()
    maven {
        name = 'SpongePowered'
        url = 'https://repo.spongepowered.org/maven'
    }
}

dependencies {
    minecraft 'com.mojang:minecraft:1.20.4'
    mappings loom.officialMojangMappings()

    modImplementation libs.fabric.loader

    implementation 'dev.lukebemish.opensesame:opensesame-fabric'
    implementation 'dev.lukebemish.opensesame:testtargets'

    testRuntimeOnly libs.bundles.junit.runtime
    testImplementation libs.bundles.junit.implementation
    testImplementation 'dev.lukebemish.opensesame:testtargets'
}

opensesame.apply(sourceSets.main)
opensesame.apply(sourceSets.test)

test {
    useJUnitPlatform()
    def runDir = project.file('run')
    workingDir(runDir)
    doFirst {
        runDir.mkdirs()
    }

    testLogging {
        showStandardStreams = true
        exceptionFormat = 'full'
        showCauses = true
        showStackTraces = true
        events = ['passed', 'failed', 'skipped']
    }

    enableAssertions = true
    systemProperty('junit.platform.reporting.open.xml.enabled', 'true')
    systemProperty('junit.platform.reporting.output.dir', 'build/reports/test')
    systemProperty('opensesame.transformer.name', 'gradle-loom, applied to minecraft')
    reports {
        junitXml.required = false
        html.required = false
    }
}