plugins {
    id 'java'
    id 'fabric-loom'
    id 'dev.lukebemish.opensesame.loom'
}

group='dev.lukebemish.opensesame'

java.toolchain.languageVersion.set(JavaLanguageVersion.of(17))

loom {
    runs.removeAll()
}

opensesame.apply(sourceSets.test)

configurations {
    testSource {
        canBeResolved = true
    }
    testClasses {
        canBeResolved = false
        canBeConsumed = true
    }
}

repositories {
    mavenCentral()
}

dependencies {
    minecraft 'com.mojang:minecraft:1.20.4'
    mappings loom.officialMojangMappings()

    testSource('dev.lukebemish.opensesame:testtargets') {
        capabilities {
            requireFeature 'transformation-sources'
        }
    }
    modImplementation libs.fabric.loader

    testRuntimeOnly libs.bundles.junit.runtime
    testImplementation 'dev.lukebemish.opensesame:opensesame-core'
    testImplementation 'dev.lukebemish.opensesame:testtargets'
    testImplementation libs.asm.core
    testImplementation libs.bundles.junit.implementation
}

tasks.named('compileTestJava', JavaCompile) {
    dependsOn(configurations.testSource)
    source(configurations.testSource)
}

tasks.named('jar', Jar) {
    from sourceSets.test.output
}

artifacts {
    testClasses tasks.remapJar
}

test {
    useJUnitPlatform()

    testLogging {
        showStandardStreams = true
        exceptionFormat = 'full'
        showCauses = true
        showStackTraces = true
        events = ['passed', 'failed', 'skipped']
    }

    enableAssertions = true
    systemProperty('junit.platform.reporting.open.xml.enabled', 'true')
    systemProperty('junit.platform.reporting.output.dir', 'build/reports/test')
    systemProperty('opensesame.transformer.name', 'gradle-loom')
    reports {
        junitXml.required = false
        html.required = false
    }
}