plugins {
    id 'java'
    id 'fabric-loom'
    id 'dev.lukebemish.opensesame.loom'
}

group='dev.lukebemish.opensesame'

java.toolchain.languageVersion.set(JavaLanguageVersion.of(17))

loom {
    runs.removeAll()
}

opensesame.apply(sourceSets.test)

configurations {
    testSource {
        canBeResolved = true
    }
    testClasses {
        canBeResolved = false
        canBeConsumed = true
    }
}

repositories {
    mavenCentral()
}

dependencies {
    minecraft 'com.mojang:minecraft:1.20.4'
    mappings loom.officialMojangMappings()

    testSource('dev.lukebemish.opensesame:testtargets') {
        capabilities {
            requireFeature 'transformation-sources'
        }
    }
    modImplementation libs.fabric.loader
}

tasks.named('compileTestJava', JavaCompile) {
    dependsOn(configurations.testSource)
    source(configurations.testSource)
}

tasks.named('jar', Jar) {
    from sourceSets.test.output
}

artifacts {
    testClasses tasks.remapJar
}

testing {
    suites {
        test {
            useJUnit()

            targets.configureEach {
                testTask.configure {
                    useJUnitPlatform()

                    testLogging {
                        showStandardStreams = true
                        exceptionFormat = 'full'
                        showCauses = true
                        showStackTraces = true
                        events = ['passed', 'failed', 'skipped']
                    }
                }
            }

            dependencies {
                runtimeOnly libs.junit.engine
                implementation 'dev.lukebemish.opensesame:testtargets'
                implementation 'dev.lukebemish.opensesame:opensesame-core'
                implementation libs.asm.core
                implementation libs.junit.api
            }
        }
    }
}