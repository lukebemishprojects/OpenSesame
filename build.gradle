plugins {
    id 'maven-publish'
}

tasks.named('updateDaemonJvm') {
    jvmVersion = JavaVersion.VERSION_21
}

def targets = [
    'linux': ['x86_64', 'aarch64', 'i386', 'arm'],
    'windows': ['x86_64', 'aarch64', 'i386'],
    'macos': ['x86_64', 'aarch64']
]

def targetfinder = [
    'linux': 'libopensesamenative.so',
    'windows': 'opensesamenative.dll',
    'macos': 'libopensesamenative.dylib'
]

version = findProperty('commit_sha')

def zig = tasks.register('zig', Exec) {
    commandLine 'zig', 'build', '--summary', 'all'
}

configurations {
    consumable('natives')
}

artifacts {
    targets.each { os, archs ->
        archs.each { arch ->
            add('natives', file("zig-out/${os}-${arch}/${targetfinder[os]}")) {
                classifier="${os}-${arch}"
                builtBy zig
            }
        }
    }
}

publishing {
    publications {
        maven(MavenPublication) {
            groupId = 'dev.lukebemish'
            artifactId = 'opensesamenative'
            configurations.natives.artifacts.each {
                artifact(it)
            }
        }
    }

    repositories {
        maven {
            name = "GitHubPackages"
            url = uri("https://maven.pkg.github.com/lukebemishprojects/OpenSesame")
            credentials {
                username = System.getenv("GITHUB_ACTOR")
                password = System.getenv("GITHUB_TOKEN")
            }
        }
    }
}